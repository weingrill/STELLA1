<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FocusControl" Id="{d08d0458-4211-4cc5-9e25-2b5c152fb2d4}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM FocusControl
VAR_INPUT
	// enable the axis
	enable: BOOL;
	// Move M2 closer to M1
	inward: 	BOOL;
	// Move M2 away from M1
	outward: 	BOOL;	
	// reset the axis(-error)
	reset: 		BOOL;
	// move the axis to new position
	MoveAxis:	BOOL;
	// calibrate the focus axis
	HomeAxis:	BOOL;
	// focus position in millimeters
	position: 		LREAL;
END_VAR

VAR_OUTPUT
	// true if absolute calibration
	Calibrated:	BOOL;
	// true if error
	Error: 		BOOL;
	// ID of NC axis error
	ErrorID:	UDINT;
	// ready signal = drive ready + standstill
	Ready:	BOOL;
END_VAR

VAR
	FocusDelay:			TON;
	FocusAxis:			FB_Axis2;
	actual_position:	LREAL;
	Focus_max:			LREAL;
	FocusUnlock:		BOOL;
	FocusUnlocked:		BOOL;
	FocusCalibration:	MC_SetPosition;
	MQTTTimer : 		TON := (PT:=T#5S);
	getlastposition:	BOOL := TRUE;
	FocusAxisEvent:		FB_EventLog;
	CalibrationEvent:	FB_EventLog;
	last_position:		LREAL := -1.0;
	HomingMode: 		MC_HOMINGMODE := 		MC_DefaultHoming;
END_VAR

VAR CONSTANT
	// reference position for homing
	homing_position:		LREAL := 92.8936; // 106.5936 - 13.7000
	calibration_position:	LREAL := 0.0;
	tolerance:				LREAL := 0.005;
END_VAR
VAR PERSISTENT
	last_calibrationposition:	LREAL := -1.0;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
Focus Control Unit
spindle gear: 5mm
brake: 86 61103H00 Var. 0005 Index C; 24VDC geglattet 0,25 A P1672/R7
motor: Faulhaber ; Schönaich ; 3557K024CR; 454  431 ; Made in germany ==> 5300 rpm
gear: Faulhaber ; MINIMOTOR SA ; swiss made ; 38/1S  43:1 ; 033896  503
Focus is locked by default
*)

GVL_Telescope.focus_unlock := FocusUnlock OR enable;

// wait until the focus is unlocked for n milliseconds
FocusDelay(IN := GVL_Telescope.focus_unlock, PT := T#500MS, Q => FocusUnlocked);

// if we are on automatic mode then position the filterwheel
IF Pendantcontrol.telescope_mode = E_TelescopeMode.automatic THEN
	IF FocusAxis.Calibrated THEN
		IF position <> last_position THEN
			IF FocusAxis.Ready THEN
				MoveAxis := TRUE;
			ELSE
				Enable := TRUE;
			END_IF
		END_IF
		IF ABS(position - actual_position) > tolerance THEN
			;
		END_IF
		IF FocusAxis.MoveDone AND FocusAxis.StandStill THEN
			position := actual_position;
			last_position := position;
			Enable := FALSE;
		END_IF
	ELSE // not calibrated
		position := calibration_position;
		IF FocusAxis.Ready THEN
			HomeAxis := TRUE;
		END_IF
	END_IF
END_IF


IF HomeAxis THEN
	IF last_calibrationposition > 0.0 THEN
		// calibrate on stored position
		HomingMode := MC_ForceCalibration;
		position := last_calibrationposition;
	ELSE
		// calibrate on limit switch
		HomingMode := MC_DefaultHoming;
		position := homing_position;
	END_IF
END_IF

// deactivate limit switches on homing
MC_SetAcceptBlockedDriveSignal(
	Axis := GVL_Telescope.FocusAxisRef, 
	Enable := HomeAxis);

FocusAxis(	
	Enable := 			FocusUnlocked,
	Reset := 			reset,
	MoveAxis := 		MoveAxis AND FocusUnlocked,
	HomeAxis :=			HomeAxis AND FocusUnlocked,
	HomingMode :=		HomingMode,
	Position := 		position,
	Velocity :=			1.0,
	Enable_Positive := 	NOT GVL_Telescope.focus_limit_far OR HomeAxis,
	Enable_Negative := 	NOT GVL_Telescope.focus_limit_near,
	Jog_Forward :=		outward AND FocusUnlocked,
	Jog_Backwards := 	inward AND FocusUnlocked,
	bCalibrationCam :=  GVL_Telescope.focus_limit_far,
	ActualPosition => 	actual_position,
	Error =>			error,
	ErrorID =>			ErrorID,
	AxisRef :=			GVL_Telescope.FocusAxisRef);	

IF FocusAxis.MoveDone THEN
	MoveAxis := FALSE;
END_IF

IF FocusAxis.HomeDone THEN
	HomeAxis := FALSE;
	Enable := FALSE;
	last_position := position;
END_IF
	
IF FocusAxis.ResetDone THEN
	reset := FALSE;
END_IF

IF FocusAxis.Calibrated OR Error THEN
	HomeAxis := FALSE;
END_IF

Calibrated := FocusAxis.Calibrated;

IF Calibrated AND actual_position < 0.0 AND last_calibrationposition > 0.0 THEN
	FocusCalibration.Execute := TRUE;
END_IF

IF FocusCalibration.Done THEN
	FocusCalibration.Execute := FALSE;
END_IF

FocusCalibration(Axis := GVL_Telescope.FocusAxisRef,
					Position := last_calibrationposition);
			

IF Calibrated AND actual_position > 0.0 THEN // AND NOT enable
	last_calibrationposition := actual_position;
	//getlastposition := FALSE;
END_IF
	
IF GVL_Telescope.focus_limit_far THEN
	Focus_max := actual_position;
END_IF

Ready := FocusAxis.Ready AND FocusAxis.StandStill;

MQTTTimer(IN:=TRUE);
IF MQTTTimer.Q THEN // publish new payload every second
	MQTTTimer(IN:=FALSE);
	MAIN.MQTTClient.Publish('telescope', 'dome', 'FocusPosition', LREAL_TO_FMTSTR(actual_position, 5, TRUE));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'FocusCalibrated', BOOL_TO_STRING(Calibrated));	
	MAIN.MQTTClient.Publish('telescope', 'dome', 'FocusReady', BOOL_TO_STRING(Ready));	
END_IF

FocusAxisEvent(	
	Trigger := 		FocusAxis.Error, 
	Level := 		ADSLOG_MSGTYPE_ERROR,
	FormatString :=	'Focus Axis Error: %s',
	OnMessage := 	NCError_TO_STRING(ErrorID),
	OffMEssage := 	'OK',
	OffLevel := 	ADSLOG_MSGTYPE_HINT);

CalibrationEvent(
	Trigger :=	Calibrated,
	Level :=	ADSLOG_MSGTYPE_HINT,
	OnMessage := 'Focus is calibrated.'
	);]]></ST>
    </Implementation>
    <LineIds Name="FocusControl">
      <LineId Id="359" Count="0" />
      <LineId Id="361" Count="0" />
      <LineId Id="360" Count="0" />
      <LineId Id="362" Count="0" />
      <LineId Id="364" Count="0" />
      <LineId Id="363" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="310" Count="0" />
      <LineId Id="638" Count="0" />
      <LineId Id="436" Count="1" />
      <LineId Id="112" Count="0" />
      <LineId Id="110" Count="0" />
      <LineId Id="832" Count="1" />
      <LineId Id="803" Count="1" />
      <LineId Id="834" Count="0" />
      <LineId Id="837" Count="2" />
      <LineId Id="841" Count="0" />
      <LineId Id="835" Count="1" />
      <LineId Id="805" Count="0" />
      <LineId Id="812" Count="2" />
      <LineId Id="824" Count="0" />
      <LineId Id="840" Count="0" />
      <LineId Id="815" Count="8" />
      <LineId Id="802" Count="0" />
      <LineId Id="377" Count="0" />
      <LineId Id="379" Count="0" />
      <LineId Id="864" Count="0" />
      <LineId Id="869" Count="2" />
      <LineId Id="865" Count="0" />
      <LineId Id="874" Count="0" />
      <LineId Id="872" Count="1" />
      <LineId Id="866" Count="0" />
      <LineId Id="381" Count="4" />
      <LineId Id="378" Count="0" />
      <LineId Id="847" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="176" Count="0" />
      <LineId Id="91" Count="1" />
      <LineId Id="175" Count="0" />
      <LineId Id="858" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="239" Count="0" />
      <LineId Id="94" Count="1" />
      <LineId Id="106" Count="1" />
      <LineId Id="177" Count="1" />
      <LineId Id="97" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="179" Count="6" />
      <LineId Id="877" Count="0" />
      <LineId Id="876" Count="0" />
      <LineId Id="186" Count="4" />
      <LineId Id="368" Count="0" />
      <LineId Id="370" Count="2" />
      <LineId Id="426" Count="1" />
      <LineId Id="878" Count="3" />
      <LineId Id="888" Count="0" />
      <LineId Id="890" Count="1" />
      <LineId Id="889" Count="0" />
      <LineId Id="884" Count="3" />
      <LineId Id="315" Count="0" />
      <LineId Id="842" Count="0" />
      <LineId Id="845" Count="1" />
      <LineId Id="844" Count="0" />
      <LineId Id="543" Count="0" />
      <LineId Id="192" Count="0" />
      <LineId Id="320" Count="1" />
      <LineId Id="626" Count="0" />
      <LineId Id="434" Count="0" />
      <LineId Id="557" Count="0" />
      <LineId Id="559" Count="3" />
      <LineId Id="564" Count="0" />
      <LineId Id="574" Count="0" />
      <LineId Id="558" Count="0" />
      <LineId Id="322" Count="0" />
      <LineId Id="193" Count="5" />
      <LineId Id="156" Count="0" />
      <LineId Id="484" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="486" Count="2" />
      <LineId Id="485" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>